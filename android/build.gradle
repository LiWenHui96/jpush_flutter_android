import org.yaml.snakeyaml.Yaml

File flutter_project_dir = rootProject.projectDir.parentFile
def cfg = new Yaml().load(new File(flutter_project_dir, "pubspec.yaml").text)

// JPush 厂商通道信息
Boolean jPushHuaweiEnable = cfg.jpush_android?.huawei?.enable
if (jPushHuaweiEnable == null) jPushHuaweiEnable = true
Boolean jPushXiaomiEnable = cfg.jpush_android?.xiaomi?.enable
if (jPushXiaomiEnable == null) jPushXiaomiEnable = true
Boolean jPushMeizuEnable = cfg.jpush_android?.meizu?.enable
if (jPushMeizuEnable == null) jPushMeizuEnable = true
Boolean jPushVivoEnable = cfg.jpush_android?.vivo?.enable
if (jPushVivoEnable == null) jPushVivoEnable = true
Boolean jPushOppoEnable = cfg.jpush_android?.oppo?.enable
if (jPushOppoEnable == null) jPushOppoEnable = true
Boolean jPushHonorEnable = cfg.jpush_android?.honor?.enable
if (jPushHonorEnable == null) jPushHonorEnable = true

group = "com.jpush.flutter.android"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "1.8.22"
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url "https://developer.huawei.com/repo/" }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.1.0")
        classpath "org.yaml:snakeyaml:2.3"
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath "com.huawei.agconnect:agcp:1.9.1.301"
    }
}

// 本地maven
String mavenLocalPath = project(":jpush_flutter_android").mkdir("repository").absolutePath

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://developer.huawei.com/repo/" }
        maven { url mavenLocalPath }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.huawei.agconnect"
apply plugin: "maven-publish"

// 定义多个 AAR 的发布配置
def aarConfigs = [
        [
                artifactId: "heytap-msp-push",
                version   : "3.5.3",
                file      : "com.heytap.msp-push-3.5.3.aar"
        ],
        [
                artifactId: "HiPushSDK",
                version   : "8.0.12.307",
                file      : "HiPushSDK-8.0.12.307.aar"
        ],
        [
                artifactId: "push-internal",
                version   : "5.0.3",
                file      : "push-internal-5.0.3.aar"
        ]
]

// 定义发布到本地 Maven 的任务
// aar文件更改后，运行 ./gradlew publish
publishing {
    publications {
        aarConfigs.each { config ->
            register(config.artifactId, MavenPublication) {
                groupId = "com.jpush.flutter"
                artifactId = config.artifactId
                version = config.version
                artifact "$projectDir/libs/${config.file}"
            }
        }
    }

    // 显式指定本地 Maven 仓库路径
    repositories {
        maven { url = mavenLocalPath }
    }
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace = "com.jpush.flutter.android"
    }

    compileSdk = 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 19

        // library 混淆 -> 随 library 引用，自动添加到 apk 打包混淆
        consumerProguardFiles "proguard-rules.pro"
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")

        // 接入 华为 厂商
        if (jPushHuaweiEnable) {
            implementation "com.huawei.hms:push:6.13.0.300"
            implementation "cn.jiguang.sdk.plugin:huawei:5.7.0"
        }

        // 接入 小米 厂商
        if (jPushXiaomiEnable) {
            implementation "cn.jiguang.sdk.plugin:xiaomi:5.7.0"
        }

        // 接入 魅族 厂商
        if (jPushMeizuEnable) {
            implementation "cn.jiguang.sdk.plugin:meizu:5.7.0"
            implementation "com.jpush.flutter:push-internal:5.0.3"
        }

        // 接入 vivo 厂商
        if (jPushVivoEnable) {
            implementation "cn.jiguang.sdk.plugin:vivo:5.7.0"
        }

        // 接入 oppo 厂商
        if (jPushOppoEnable) {
            implementation "cn.jiguang.sdk.plugin:oppo:5.7.0"
            implementation "com.jpush.flutter:heytap-msp-push:3.5.3"
            implementation "com.google.code.gson:gson:2.6.2"
            implementation "commons-codec:commons-codec:1.6"
            implementation "androidx.annotation:annotation:1.1.0"
        }

        // 接入 荣耀 厂商
        if (jPushHonorEnable) {
            implementation "cn.jiguang.sdk.plugin:honor:5.7.0"
            implementation "com.jpush.flutter:HiPushSDK:8.0.12.307"
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}
